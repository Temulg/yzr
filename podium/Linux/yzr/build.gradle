import temulg.yzr.gradle.tasks.BootstrapEncoder;

plugins {
	id 'cpp-application'
}

def javaHome = org.gradle.internal.jvm.Jvm.current().javaHome

task genBootstrap(
	type: BootstrapEncoder,
	dependsOn: ':podium:Linux:bootstrap:compileJava'
) {
	source tasks.getByPath(':podium:Linux:bootstrap:compileJava').outputs
	destDir = project.layout.buildDirectory.dir("generated/cpp")
}

application {
	source.from = ['src/main/cpp', genBootstrap.destDir]
}

tasks.withType(CppCompile) {
	compilerArgs = [
		"-Werror",
		"-std=c++14",
		"-I${javaHome}/include",
		"-I${javaHome}/include/linux"
	]
}

tasks.withType(LinkExecutable) {
	linkerArgs = [
		"-static-libstdc++",
		"-L${javaHome}/lib/server",
		"-Wl,-rpath,${javaHome}/lib/server",
		"-ljvm"
	]
}

task install(type: Copy, dependsOn: 'linkRelease') {
	from buildDir.toPath().resolve('exe/main/release')
	into rootProject.projectDir.toPath().resolve("image/bin")
}
